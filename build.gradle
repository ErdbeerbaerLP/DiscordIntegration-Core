buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version "7.1.2"
}

//Build variables
version = "2.6.2"
group = "de.erdbeerbaerlp.dcintegration.common"
archivesBaseName = "dcintegration-core"


//Gradle Repositories
repositories {
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven { url 'https://jitpack.io' }
    mavenCentral()

    flatDir {
        dirs 'libs'
    }

}
//ShadowJar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
shadowJar {
    //noinspection GroovyAccessibility
    archiveClassifier = ""
    configurations = [project.configurations.embed]
    exclude('META-INF/services/**')
    relocate 'okhttp3', 'dcshadow.okhttp'
    relocate 'okio', 'dcshadow.okio'
    relocate 'club.minnced', 'dcshadow.club.minnced'
    relocate 'com.sun', 'dcshadow.com.sun'
    relocate 'com.iwebpp', 'dcshadow.com.iwebpp'
    relocate 'com.neovisionaries', 'dcshadow.com.neovisionaries'
    relocate 'gnu', 'dcshadow.gnu'
    relocate 'org.apache.commons.collections4', 'dcshadow.org.apache.commons.collections4'
    relocate('org.apache.commons.lang3', 'dcshadow.org.apache.commons.lang3')
    relocate 'org.json', 'dcshadow.org.json'
    relocate 'org.intellij', 'dcshadow.org.intellij'
    relocate 'org.jetbrains', 'dcshadow.org.jetbrains'
    relocate 'tomp2p', 'dcshadow.tomp2p'
    relocate 'com.fasterxml', 'dcshadow.com.fasterxml'
    relocate 'dev.vankka', 'dcshadow.dev.vankka'
    relocate 'net.kyori', 'dcshadow.net.kyori'
    relocate 'org.checkerframework', 'dcshadow.org.checkerframework'
    relocate "com.moandjiezana", "dcshadow.com.moandjiezana"
    relocate "ch.lambdaj", "dcshadow.ch.lambdaj"
    relocate "emoji4j", "dcshadow.emoji4j"
    relocate "javax.annotation", "dcshadow.javax.annotation"
    relocate "net.sf", "dcshadow.net.sf"
    relocate "org.hamcrest", "dcshadow.org.hamcrest"
    relocate "org.objenesis", "dcshadow.org.objenesis"
    relocate "kotlin", "dcshadow.kotlin"
    relocate "com.vdurmont", "dcshadow.com.vdurmont"
}
//Dependencies
dependencies {

    // =========  Common  =====================

    //Discord libraries
    embed 'net.dv8tion:JDA:5.0.0-beta.5'
    embed 'club.minnced:discord-webhooks:0.8.2'

    //Emoji-Java
    embed('com.vdurmont:emoji-java:5.1.1')

    //Minecraft <==> Discord conversion
    embed 'net.kyori:adventure-api:4.12.0'
    embed('net.kyori:adventure-text-serializer-gson:4.12.0'){
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    embed "dev.vankka:mcdiscordreserializer:4.3.0"
    embed 'dev.vankka:SimpleAST:2.2.8'
    embed 'net.kyori:adventure-text-serializer-legacy:4.12.0'


    compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    //Toml4j
    embed("de.erdbeerbaerlp:toml4j:cf5b4655"){
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    //embed("Toml4J:toml4j-0.7.5-SNAPSHOT")

    //Apache commons lang3
    embed group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    //SQLite
    embed 'org.xerial:sqlite-jdbc:3.40.1.0'

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')

    //Logger
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.19.0'

}
tasks.build.dependsOn(tasks.shadowJar)

java {
    withJavadocJar()
    withSourcesJar()
}
//Manifest attributes
jar {
    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}
javadoc {
    options.author()
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    options.addBooleanOption('Xdoclint:none', true)
    failOnError = false
}
artifacts {
    archives tasks.shadowJar
}
publishing {
 if (project.hasProperty('nexusUser')) { // $GRADLE_USER_HOME/gradle.properties
    publications {
        maven(MavenPublication) {
            groupId = 'de.erdbeerbaerlp'
            artifactId = 'dcintegration.common'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {// $GRADLE_USER_HOME/gradle.properties
                username = project.nexusUser
                password = project.nexusPass
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url = "https://repo.erdbeerbaerlp.de/repository/maven-snapshots"
            }else{
                url = "https://repo.erdbeerbaerlp.de/repository/maven-releases"
            }
        }
    }
    }
}
