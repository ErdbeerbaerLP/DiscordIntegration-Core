buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version "7.1.0"
}

//Build variables
version = "2.3.5"
group = "de.erdbeerbaerlp.dcintegration.common"
archivesBaseName = "dcintegration-core"


//Gradle Repositories
repositories {
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://nexus.vankka.dev/repository/maven-public/" }
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'howaner'
        url = "https://repo.howaner.de/"
    }

}
//ShadowJar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
shadowJar {
    classifier = ""
    configurations = [project.configurations.embed]
    exclude('META-INF/services/**')
    relocate 'okhttp3', 'dcshadow.okhttp'
    relocate 'okio', 'dcshadow.okio'
    relocate 'club.minnced', 'dcshadow.club.minnced'
    relocate 'com.sun', 'dcshadow.com.sun'
    relocate 'com.iwebpp', 'dcshadow.com.iwebpp'
    relocate 'com.google.gson', 'dcshadow.com.google.gson'
    relocate 'com.neovisionaries', 'dcshadow.com.neovisionaries'
    relocate 'gnu', 'dcshadow.gnu'
    relocate 'org.apache.commons.collections4', 'dcshadow.org.apache.commons.collections4'
    relocate('org.apache.commons.lang3', 'dcshadow.org.apache.commons.lang3')
    relocate 'org.json', 'dcshadow.org.json'
    relocate 'org.intellij', 'dcshadow.org.intellij'
    relocate 'org.jetbrains', 'dcshadow.org.jetbrains'
    relocate 'tomp2p', 'dcshadow.tomp2p'
    relocate 'com.fasterxml', 'dcshadow.com.fasterxml'
    relocate 'dev.vankka', 'dcshadow.dev.vankka'
    relocate 'net.kyori', 'dcshadow.net.kyori'
    relocate 'org.checkerframework', 'dcshadow.org.checkerframework'
    relocate "com.moandjiezana", "dcshadow.com.moandjiezana"
    relocate "ch.lambdaj", "dcshadow.ch.lambdaj"
    relocate "emoji4j", "dcshadow.emoji4j"
    relocate "javax.annotation", "dcshadow.javax.annotation"
    relocate "net.sf", "dcshadow.net.sf"
    relocate "org.hamcrest", "dcshadow.org.hamcrest"
    relocate "org.objenesis", "dcshadow.org.objenesis"
    relocate "kotlin", "dcshadow.kotlin"
    relocate "com.vdurmont", "dcshadow.com.vdurmont"
}
artifacts {
    archives tasks.shadowJar
}
//Dependencies
dependencies {

    // =========  Common  =====================

    //Discord libraries
    embed("net.dv8tion:JDA:4.4.0_351") {
        exclude group: 'okhttp3', module: "okhttp"
    }
    embed "com.squareup.okhttp3:okhttp:4.9.3"
    embed 'club.minnced:discord-webhooks:0.7.2'

    //GSON
    embed group: 'com.google.code.gson', name: 'gson', version: '2.8.9'


    //Emoji-Java
    embed('com.vdurmont:emoji-java:5.1.1')

    //Minecraft <==> Discord conversion
    embed 'net.kyori:adventure-api:4.9.3'
    embed('net.kyori:adventure-text-serializer-gson:4.9.3')
    embed "dev.vankka:MCDiscordReserializer:4.2.2"
    embed "dev.vankka:SimpleAST:2.2.7"
    embed "net.kyori:adventure-text-serializer-legacy:4.9.3"

    //Toml4j
    embed("de.erdbeerbaerlp:toml4j:cf5b4655")

    //Apache commons lang3
    embed group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    //SLF4J Simple Binding
    embed group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.35'

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')

}
tasks.build.dependsOn(tasks.shadowJar)
//Manifest attributes
jar {
    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}